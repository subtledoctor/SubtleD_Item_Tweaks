
//__________________________________________________________________________________
//__________________________________________________________________________________
//
//								ITEM TWEAKS
//__________________________________________________________________________________
//__________________________________________________________________________________


//DEFINE_ACTION_FUNCTION personal_item_tweaks BEGIN


//__________________________________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_spell_weap_apr BEGIN
// spell weapon APR
// find all items, set base APR to 2
COPY_EXISTING ~shillel.itm~ ~override~
				~shille.itm~ ~override~
				~shille2.itm~ ~override~
				~shille3.itm~ ~override~
				~fblade.itm~ ~override~
				~fblade1.itm~ ~override~
				~fblade2.itm~ ~override~
				~fblade3.itm~ ~override~
				~fblade4.itm~ ~override~
				~fblade5.itm~ ~override~
				~fblade6.itm~ ~override~
				~fblade7.itm~ ~override~
				~fblade8.itm~ ~override~
				~fblade9.itm~ ~override~
				~shamme1.itm~ ~override~
				~shamme2.itm~ ~override~
				~shamme3.itm~ ~override~
				~shammr.itm~ ~override~
				~shammr2.itm~ ~override~
				~shammr3.itm~ ~override~
				~shammr4.itm~ ~override~
				~shammr5.itm~ ~override~
				~b_iceb1.itm~ ~override~
				~b_iceb2.itm~ ~override~
				~b_iceb3.itm~ ~override~
				~b_iceb4.itm~ ~override~
				~b_iceb5.itm~ ~override~
				~b_iceb6.itm~ ~override~
				~b_iceb7.itm~ ~override~
				~b_iceb8.itm~ ~override~
				~b_iceb9.itm~ ~override~
				~b_iceb10.itm~ ~override~
				~moonbla.itm~ ~override~
				~cdimoonb.itm~ ~override~
				~smcudge.itm~ ~override~
				~cdismcud.itm~ ~override~
				~decasta.itm~ ~override~
				~cdideca.itm~ ~override~
				~phanblad.itm~ ~override~
				~blakblad.itm~ ~override~
				~msword.itm~ ~override~
	LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 1 target = 1 parameter1 = 2 parameter2 = 1 timing = 2 END
IF_EXISTS BUT_ONLY

OUTER_SET spell_ios = IDS_OF_SYMBOL (~spell~ ~CLERIC_STONE_FIST~)
ACTION_IF !(spell_ios = 0 - 1) BEGIN
  LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = ~CLERIC_STONE_FIST~ RET spell_res END
  OUTER_TEXT_SPRINT stone_fist_res ~%spell_res%~
  ACTION_IF (FILE_EXISTS_IN_GAME ~%stone_fist_res%.itm~) BEGIN
	COPY_EXISTING ~%stone_fist_res%.itm~ ~override~
	  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 1 target = 1 parameter1 = 2 parameter2 = 1 timing = 2 END
  END
END

END

//__________________________________________________________________________________


// ring of danger sense_____________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_ring_danger_sense BEGIN
// AC, backstab immunity, spell evasion...?
COPY_EXISTING ~ring36.itm~ ~override~
  SAY IDENTIFIED_DESC @30102
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 292 target = 1 parameter2 = 1 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 0 target = 1 parameter1 = 1 parameter2 = 0 timing = 2 END
IF_EXISTS BUT_ONLY
// 
END
//__________________________________________________________________________________


// gloves of missile snaring________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_gloves_missile_snaring BEGIN
// block 1 missile per round

LAF d5_resolve_state STR_VAR new_state_id = ~D5_SNAREARROW~ RET new_state_ind END
OUTER_SET missile_snaring_state = %new_state_ind%
OUTER_SET snare_string = RESOLVE_STR_REF (~Deflect~)

ACTION_IF !(FILE_EXISTS_IN_GAME ~7eyes.2da~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/7eyes.2da~ ~override~
END

APPEND ~7eyes.2da~ ~SNARE_ARROW  %missile_snaring_state%      %snare_string%      12*0x800000 *           *           *            *            *          *          *          * ~ UNLESS ~SNARE_ARROW~

COPY_EXISTING ~7eyes.2da~ ~override~
  LPF 2DA_MISSING_COLS END

COPY_EXISTING ~7eyes.2da~ ~override~
  COUNT_2DA_ROWS 1 rows
  FOR (row = 1; row < rows; ++row) BEGIN
	READ_2DA_ENTRY row 0 1 tag
	PATCH_IF (~%tag%~ STRING_EQUAL_CASE ~SNARE_ARROW~) BEGIN
	  SET snare_row = row
	END
  END
BUT_ONLY

ACTION_IF !(FILE_EXISTS_IN_GAME ~d5misgl.eff~) BEGIN
  CREATE EFF ~d5misgl~
    WRITE_LONG 0x10 232
    WRITE_LONG 0x14 1
    WRITE_LONG 0x18 0
    WRITE_LONG 0x1c 0
    WRITE_LONG 0x20 20
    WRITE_LONG 0x24 2
    WRITE_LONG 0x28 0
    WRITE_SHORT 0x2c 100
    WRITE_ASCII 0x30 ~D5MISGL1~ #8
    WRITE_LONG 0x48 102
END

ACTION_IF !(FILE_EXISTS_IN_GAME ~d5misgl1.spl~) BEGIN
  COPY ~%MOD_FOLDER%/data/misc/d5_base.spl~ ~override/d5misgl1.spl~
    LPF ADD_SPELL_EFFECT INT_VAR opcode = 335 target = 1 power = 0 parameter1 = %missile_snaring_state% parameter2 = %snare_row% timing = 0 duration = 7 END
    LPF ADD_SPELL_EFFECT INT_VAR insert_point = 0 opcode = 321 target = 1 timing = 1 STR_VAR resource = ~d5misgl1~ END
END

COPY_EXISTING ~brac18.itm~ ~override~
  SAY IDENTIFIED_DESC @30202
  LPF DELETE_EFFECT INT_VAR match_opcode = 0 match_parameter2 = 2 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 83 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 305 END
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = ~spra303~ END
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = ~spwi311~ END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 335 target = 1 parameter1 = %missile_snaring_state% parameter2 = %snare_row% timing = 0 duration = 7 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter2 = 2 timing = 2 STR_VAR resource = ~d5misgl~ END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 0 target = 1 parameter1 = 3 parameter2 = 2 timing = 2 END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// weapons that explode on hit_______________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_detonating_weapons BEGIN

ADD_PROJECTILE ~%MOD_FOLDER%/data/misc/b_pfire.pro~ 
COPY ~%MOD_FOLDER%/data/misc/PFIREA.bam~ ~override~
COPY ~%MOD_FOLDER%/data/misc/PFIREX.bam~ ~override~
COPY ~%MOD_FOLDER%/data/misc/#PRFIRE.vvc~ ~override~
COPY ~%MOD_FOLDER%/data/misc/#EFF_P45.WAV~ ~override~
COPY ~%MOD_FOLDER%/data/misc/#ARE_P03.WAV~ ~override~

COPY ~%MOD_FOLDER%/data/misc/d5_base.spl~ ~override/d5detwp.spl~
  LPF ALTER_SPELL_HEADER INT_VAR projectile = %b_pfire% END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 318 target = 2 parameter2 = 43 timing = 0 duration = 0 STR_VAR resource = ~%SOURCE_RES%~ END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 12 target = 2 power = 4 parameter1 = 0 parameter2 = 8 timing = 1 dicenumber = 5 dicesize = 6 savingthrow = (1 << 24) END
  LPF ADD_SPELL_EFFECT INT_VAR opcode = 206 target = 2 power = 4 parameter1 = (0 - 1) timing = 0 duration = 6 STR_VAR resource = ~d5detwp~ END

COPY_EXISTING ~blun26.itm~ ~override~
			  ~blun26.itm~ ~override~
			  ~bdstaf03.itm~ ~override~
	LPF ALTER_EFFECT INT_VAR header_type = 1 match_opcode = 146 STR_VAR resource = ~d5detwp~ END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// skin of the forest_____________________________________________________________
//
DEFINE_ACTION_FUNCTION forest_leather_armor BEGIN
// recurring stoneskin, like carapace
// use alternate stonekin opcode, test against ironskin

CREATE EFF ~d5leat14~
  WRITE_LONG 0x10 232
  WRITE_LONG 0x14 1
  WRITE_LONG 0x18 0
  WRITE_LONG 0x1c 0
  WRITE_LONG 0x20 20
  WRITE_LONG 0x24 2
  WRITE_LONG 0x28 0
  WRITE_SHORT 0x2c 100
  WRITE_ASCII 0x30 ~d5bbark~ #8
  WRITE_LONG 0x48 102

COPY_EXISTING ~leat14.itm~ ~override~
  SAY IDENTIFIED_DESC @30402
  LPF DELETE_EFFECT INT_VAR match_opcode = 206 STR_VAR match_resource = ~sppr202~ END
  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 0 match_parameter1 = 1 parameter1 = 5 END
// add skin effect
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 177 target = 1 parameter1 = 0 parameter2 = 2 timing = 2 STR_VAR resource = ~d5leat14~ END
IF_EXISTS BUT_ONLY

COPY ~%MOD_FOLDER%/data/item_tweaks/d5bbark.spl~ ~override~

// allow opcode 314 to function alongside the juggernaut golem manual
COPY_EXISTING ~tomegol4.bcs~ ~override~
  DECOMPILE_BCS_TO_BAF
	REPLACE_TEXTUALLY ~!CheckStatGT(LastSummonerOf(Myself),0,STONESKINSGOLEM)~ ~~
	REPLACE_TEXTUALLY ~!CheckStatGT(Player[1-6],0,STONESKINSGOLEM)~ ~~
	REPLACE_TEXTUALLY ~Kill(Myself)~ ~~
  COMPILE_BAF_TO_BCS
IF_EXISTS BUT_ONLY

COPY_EXISTING ~tomegol4.cre~ ~override~
  WRITE_ASCII 0x248 ~~
IF_EXISTS BUT_ONLY

// remove awful sound
ACTION_IF !(FILE_EXISTS_IN_GAME ~d5eff02.wav~) BEGIN
  COPY_EXISTING ~eff_e02.wav~ ~override/d5eff02.wav~
  COPY ~%MOD_FOLDER%/data/item_tweaks/blank.wav~ ~override/eff_e02.wav~
  COPY_EXISTING_REGEXP GLOB ~^.+\.spl$~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 174 STR_VAR match_resource = ~eff_e02~ resource = ~d5eff02~ END
    END
  BUT_ONLY
  COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
    PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
	  LPF ALTER_EFFECT INT_VAR silent = 1 match_opcode = 174 STR_VAR match_resource = ~eff_e02~ resource = ~d5eff02~ END
    END
  BUT_ONLY
END

END
//__________________________________________________________________________________


// IR rod of absorption_____________________________________________________________
//
DEFINE_ACTION_FUNCTION instant_rod_absorb BEGIN

COPY_EXISTING ~rods01.itm~ ~override~
	LPF ALTER_SPELL_HEADER INT_VAR target = 7 END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________

DEFINE_ACTION_FUNCTION longer_rod_absorb BEGIN

COPY_EXISTING ~rods01.spl~ ~override~
	LPF ALTER_EFFECT INT_VAR match_opcode = 259 duration = 60 END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// Badge of the Brave_______________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_brave_badge BEGIN

COPY_EXISTING ~amulbra.itm~ ~override~
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 161 target = 1 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 106 target = 1 parameter1 = 1 parameter2 = 1 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 23 target = 1 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 target = 1 parameter2 = 106 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 target = 1 parameter2 = 24 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 101 target = 1 parameter2 = 23 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 169 target = 1 parameter2 = 36 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 240 target = 1 parameter2 = 36 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 267 target = 1 parameter1 = 14007 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 267 target = 1 parameter1 = 17427 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 328 target = 1 parameter2 = 106 timing = 2 END
  LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 278 target = 1 parameter1 = 1 parameter2 = 0 timing = 2 END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// Gargoyle Cloak___________________________________________________________________
//
DEFINE_ACTION_FUNCTION gargoyle_cloak BEGIN

ACTION_IF !(FILE_EXISTS_IN_GAME ~boot12.spl~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/boot12.spl~ ~override~
END

COPY_EXISTING ~bdclck05.itm~ ~override~
  SAY IDENTIFIED_DESC @30602
  LPF ALTER_ITEM_HEADER INT_VAR header_type = 3 charges = 2 END
  LPF ALTER_EFFECT INT_VAR match_opcode = 146 STR_VAR resource = ~boot12~ END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// Dwarven Thrower__________________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_dwarven_thrower BEGIN

COPY_EXISTING ~hamm06.itm~ ~override~
			  ~hamm06a.itm~ ~override~
			  ~hamm06b.itm~ ~override~
	WRITE_BYTE 0x20 (THIS BAND 0b01111111)
	WRITE_BYTE 0x21 (THIS BAND 0b01100000)
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// Methild's Harp___________________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_instruments BEGIN

COPY_EXISTING ~misc3n.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = ~sppr201~ END
    FOR (index = 0x54 ; index >= 0x50 ; index -= 4) BEGIN
	READ_LONG "%index%" "valid"
	PATCH_IF ("%valid%" < 2147483646) AND ("%valid%" >= 0) BEGIN
	  READ_STRREF "%index%" "description"
	  SPRINT rf @30801
	  SPRINT rfa @30802
	  PATCH_IF (~%description%~ STRING_CONTAINS_REGEXP ~%rf%~) = 0 BEGIN
		INNER_PATCH_SAVE new_desc ~%description%~ BEGIN
		  REPLACE_TEXTUALLY ~%rf%~ ~%rfa%~
		END
		SAY_EVALUATED "%index%" ~%new_desc%~
	  END
	END
  END
IF_EXISTS BUT_ONLY

COPY_EXISTING ~misc3o.itm~ ~override~
  LPF ADD_ITEM_EFFECT INT_VAR opcode = 146 target = 2 parameter2 = 1 timing = 1 STR_VAR resource = ~sppr307~ END
    FOR (index = 0x54 ; index >= 0x50 ; index -= 4) BEGIN
	READ_LONG "%index%" "valid"
	PATCH_IF ("%valid%" < 2147483646) AND ("%valid%" >= 0) BEGIN
	  READ_STRREF "%index%" "description"
	  SPRINT rp @30811
	  SPRINT rprc @30812
	  SPRINT rpbe @30813
	  PATCH_IF (~%description%~ STRING_CONTAINS_REGEXP ~%rp%~) = 0 BEGIN
		INNER_PATCH_SAVE new_desc ~%description%~ BEGIN
		  PATCH_IF !(FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
			REPLACE_TEXTUALLY ~%rp%~ ~%rprc%~
		  END
		  PATCH_IF (FILE_EXISTS_IN_GAME ~dvsrv4here.mrk~) BEGIN
			REPLACE_TEXTUALLY ~%rp%~ ~%rpbe%~
		  END
		END
		SAY_EVALUATED "%index%" ~%new_desc%~
	  END
	END
  END
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// Edwin's Amulet___________________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_edwin_amulet INT_VAR slot_reduction = 0 level_reduction = 0 BEGIN

COPY_EXISTING ~misc89.itm~ ~override~
  PATCH_IF slot_reduction = 1 BEGIN
	SAY UNIDENTIFIED_DESC @30912
	LPF ALTER_EFFECT INT_VAR match_opcode = 42 match_parameter1 = 2 parameter1 = 1 END
  END
  PATCH_IF level_reduction = 1 BEGIN
	SAY UNIDENTIFIED_DESC @30922
	LPF DELETE_EFFECT INT_VAR match_opcode = 42 match_parameter2 = 128 END
	LPF DELETE_EFFECT INT_VAR match_opcode = 42 match_parameter2 = 256 END
  END  
IF_EXISTS BUT_ONLY

END
//__________________________________________________________________________________


// cloak of the weave_______________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_cloak_of_the_weave BEGIN

COPY ~%MOD_FOLDER%/data/item_tweaks/clck15.itm~ ~override~
  SAY NAME2 @31001
  SAY IDENTIFIED_DESC @31002

END
//__________________________________________________________________________________


// free casting for some items______________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_casting_items BEGIN

// cloak of the weave
// robe of enfusing
// evermemory (if not evermemory-->acuity)
// edion's/kontick's rings
// bard's/wondrous gloves
// band of the forest
// ring of holiness?
// holy symbols??

ACTION_IF (FILE_EXISTS_IN_GAME ~urobe1a.itm~) BEGIN
  COPY_EXISTING ~urobe1a.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~z0robe99.itm~) BEGIN
  COPY_EXISTING ~z0robe99.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~clck15.itm~) BEGIN
  COPY_EXISTING ~clck15.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~brac24.itm~) BEGIN
  COPY_EXISTING ~brac24.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~brac25.itm~) BEGIN
  COPY_EXISTING ~brac25.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ring03.itm~) BEGIN
  COPY_EXISTING ~ring03.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5shm-1~ END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ring22.itm~) BEGIN
  COPY_EXISTING ~ring22.itm~ ~override~
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5shm-1~ END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
  COPY_EXISTING ~ring08_.itm~ ~override~
    LPF DELETE_ITEM_HEADER INT_VAR header_type = 3 END
//    LPF DELETE_EFFECT INT_VAR match_opcode = 177 STR_VAR match_resource = ~d5rng8~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
END

ACTION_IF !(FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
 ACTION_IF !(MOD_IS_INSTALLED ~item_rev.tp2~ ~0~) BEGIN
  COPY_EXISTING ~ring40.itm~ ~override~
    LPF DELETE_ITEM_HEADER INT_VAR header_type = 3 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 177 parameter1 = 19 parameter2 = 5 STR_VAR match_resource = ~d5rng8~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
 END
 ACTION_IF (MOD_IS_INSTALLED ~item_rev.tp2~ ~0~) OR (GAME_IS ~bgee~) BEGIN
  COPY_EXISTING ~ring08.itm~ ~override~
    LPF DELETE_ITEM_HEADER INT_VAR header_type = 3 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 177 parameter1 = 19 parameter2 = 5 STR_VAR match_resource = ~d5rng8~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
 END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~bdring08.itm~) BEGIN
  COPY_EXISTING ~bdring08.itm~ ~override~
    LPF DELETE_ITEM_HEADER INT_VAR header_type = 3 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 177 parameter1 = 19 parameter2 = 5 STR_VAR match_resource = ~d5rng8~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ringkon.itm~) BEGIN
  COPY_EXISTING ~ringkon.itm~ ~override~
    LPF DELETE_ITEM_HEADER INT_VAR header_type = 3 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 177 parameter1 = 19 parameter2 = 5 STR_VAR match_resource = ~d5rngk~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-2~ END
  ACTION_IF (FILE_EXISTS_IN_GAME ~mh#iwd11.itm~) BEGIN
    COPY_EXISTING ~ringkon.itm~ ~override/mh#iwd11.itm~
  END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ringed.itm~) BEGIN
  COPY_EXISTING ~ringed.itm~ ~override~
    LPF DELETE_ITEM_HEADER INT_VAR header_type = 3 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 177 parameter1 = 19 parameter2 = 5 STR_VAR match_resource = ~d5rnge~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-5~ END
END

ACTION_IF (FILE_EXISTS_IN_GAME ~mh#iwd11.itm~) BEGIN
//  ACTION_IF !(FILE_EXISTS_IN_GAME ~ringkon.itm~) BEGIN
    COPY_EXISTING ~mh#iwd11.itm~ ~override~
    LPF DELETE_ITEM_HEADER INT_VAR header_type = 3 END
    LPF ALTER_EFFECT INT_VAR match_opcode = 177 parameter1 = 19 parameter2 = 5 STR_VAR match_resource = ~d5rng8~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-1~ END
    LPF ADD_ITEM_EQEFFECT INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2 STR_VAR resource = ~d5src-2~ END   
//  END
END

END	//	end function
//__________________________________________________________________________________


// rings of wizardry-->sequencers___________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_rings_of_wizardry BEGIN

ACTION_IF !(FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08_.itm~ ~override/ring08.itm~
    SAY NAME2 @31111
    SAY IDENTIFIED_DESC @31112
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08_.itm~ ~override/ring08_.itm~
    SAY NAME2 @31111
    SAY IDENTIFIED_DESC @31112
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08.itm~ ~override/ring08.itm~
    SAY NAME2 @31115
    SAY IDENTIFIED_DESC @31116
END

ACTION_IF (FILE_EXISTS_IN_GAME ~bdring08.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08_.itm~ ~override/bdring08.itm~
    SAY NAME2 @31111
    SAY IDENTIFIED_DESC @31112
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ring40.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring40.itm~ ~override/ring40.itm~
    SAY NAME2 @31113
    SAY IDENTIFIED_DESC @31114
END

ACTION_IF (FILE_EXISTS_IN_GAME ~c2ring04.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/c2ring04.itm~ ~override/c2ring04.itm~
    SAY NAME2 @31117
    SAY IDENTIFIED_DESC @31118
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ringkon.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08_.itm~ ~override/ringkon.itm~
    SAY NAME2 @31111
    SAY IDENTIFIED_DESC @31112
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ringed.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08_.itm~ ~override/ringed.itm~
    SAY NAME2 @31111
    SAY IDENTIFIED_DESC @31112
END

ACTION_IF (FILE_EXISTS_IN_GAME ~mh#iwd11.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08.itm~ ~override/mh#iwd11.itm~
    SAY NAME2 @31115
    SAY IDENTIFIED_DESC @31116
END

END
//__________________________________________________________________________________


// only evermemory-->sequencer______________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_evermemory_sequencer BEGIN

ACTION_IF !(FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08_.itm~ ~override/ring08.itm~
    SAY NAME2 @31111
    SAY IDENTIFIED_DESC @31112
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08_.itm~ ~override/ring08_.itm~
    SAY NAME2 @31111
    SAY IDENTIFIED_DESC @31112
END

ACTION_IF (FILE_EXISTS_IN_GAME ~bdring08.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08_.itm~ ~override/bdring08.itm~
    SAY NAME2 @31111
    SAY IDENTIFIED_DESC @31112
END

ACTION_IF (FILE_EXISTS_IN_GAME ~ringkon.itm~) BEGIN
  COPY ~%MOD_FOLDER%/data/item_tweaks/ring08_.itm~ ~override/ringkon.itm~
    SAY NAME2 @31111
    SAY IDENTIFIED_DESC @31112
END

END
//__________________________________________________________________________________


// evermemory-->acuity______________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_evermemory_acuity BEGIN

ACTION_IF !(MOD_IS_INSTALLED ~item_rev.tp2~ ~0~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
    COPY_EXISTING ~ring40.itm~ ~override/ring08_.itm~
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~bdring08.itm~) BEGIN
    COPY_EXISTING ~ring40.itm~ ~override/bdring08.itm~
  END
END
ACTION_IF (MOD_IS_INSTALLED ~item_rev.tp2~ ~0~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
    COPY_EXISTING ~ring08.itm~ ~override/ring08_.itm~
  END
  ACTION_IF (FILE_EXISTS_IN_GAME ~bdring08.itm~) BEGIN
    COPY_EXISTING ~ring08.itm~ ~override/bdring08.itm~
  END
END

END
//__________________________________________________________________________________


// acuity-->evermemory______________________________________________________________
//
DEFINE_ACTION_FUNCTION tweak_acuity_evermemory BEGIN

ACTION_IF !(MOD_IS_INSTALLED ~item_rev.tp2~ ~0~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
    COPY_EXISTING ~ring08_.itm~ ~override/ring40.itm~
  END
END
ACTION_IF (MOD_IS_INSTALLED ~item_rev.tp2~ ~0~) BEGIN
  ACTION_IF (FILE_EXISTS_IN_GAME ~ring08_.itm~) BEGIN
    COPY_EXISTING ~ring08_.itm~ ~override/ring08.itm~
  END
END

END
//__________________________________________________________________________________


